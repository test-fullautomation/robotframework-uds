% Copyright 2020-2024 Robert Bosch GmbH

% Licensed under the Apache License, Version 2.0 (the "License");
% you may not use this file except in compliance with the License.
% You may obtain a copy of the License at

% http://www.apache.org/licenses/LICENSE-2.0

% Unless required by applicable law or agreed to in writing, software
% distributed under the License is distributed on an "AS IS" BASIS,
% WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
% See the License for the specific language governing permissions and
% limitations under the License.

% --------------------------------------------------------------------------------------------------------------

\section{Overview}
The \pkg\ is designed to interface with automotive ECUs using the UDS protocol 
over the DoIP (Diagnostic over IP) transport layer. 
This library abstracts the complexities of UDS communication, allowing users to 
focus on writing high-level test cases that validate specific diagnostic 
services and responses.

\section{UDS Connector (DoIP)}
Currently, the library supports the \rcode{DoIP} (Diagnostic over IP) transport 
layer, which is commonly used in modern vehicles for diagnostic communication. 
DoIP allows for faster data transfer rates and easier integration with 
network-based systems compared to traditional CAN-based diagnostics.

\section{Configuration}
In order to connect and send/receive message properly using the \pkg\, 
certain configurations must be set up:

\begin{itemize}
   \item DoIP Configuration:
         The library requires the IP address and port of the ECU or the gateway 
         through which the ECU is accessed.
   \item Data Identifiers and Codec:
         Define the Data Identifiers (DIDs) and corresponding codecs in the 
         libraryâ€™s configuration. 
         This enables correct encoding and decoding of data between the test 
         cases and the ECU.
   \item Session Management: 
         Some UDS services may require the ECU to be in a specific diagnostic 
         session (e.g., extended diagnostics). 
         The library should be configured to manage these session transitions 
         seamlessly.
\end{itemize}

\section{Supported UDS Services}
The \pkg\ library supports almost UDS service as defined in 
\href{https://automotive.wiki/index.php/ISO_14229}{ISO 14229}, 
providing comprehensive coverage for ECU diagnostics.

For detailed information on specific services and how to use them, please refer 
to the next section.

\section{Enhancements Usability with ODXTools Integration}
The \pkg\ library comes with 
\href{https://github.com/mercedes-benz/odxtools}{odxtools} fully integrated, 
allowing you to use readable service names instead of dealing with hex IDs.

You can now specify service names directly in your test cases, 
making them more readable and user-friendly.

\section{Multiple Connections}
The \pkg\ can be extended to manage multiple connections simultaneously.
This is beneficial when working with complex vehicle systems or simultaneously
testing multiple ECUs.

\section{Examples}
\begin{robotcode}
*** Settings ***
Library           RobotFramework_TestsuitesManagement    WITH NAME    testsuites
Library           RobotFramework_UDS
Suite Setup       Connect
Suite Teardown    Disconnect

*** Variables ***
${SUT_IP_ADDRESS}=         ecu_ip_address
${SUT_LOGICAL_ADDRESS}=    ecu_logical_address
${TB_IP_ADDRESS}=          client_ip_address
${TB_LOGICAL_ADDRESS}=     client_logical_address
${ACTIVATION_TYPE}=        0

${FILE}=       path/file.pdx
${VARIANT}=    variant

${NAME}=    UDS Connector

*** Keywords ***
Connect
    Log    Create a uds Connector
    ${uds}=    Create UDS Connector    ecu_ip_address= ${SUT_IP_ADDRESS}
    ...                                ecu_logical_address= ${SUT_LOGICAL_ADDRESS}
    ...                                client_ip_address= ${TB_IP_ADDRESS}
    ...                                client_logical_address= ${TB_LOGICAL_ADDRESS}
    ...                                activation_type= ${ACTIVATION_TYPE}
    Log    Using UDS Connector
    Connect UDS Connector    name=${Name}
    Log    Open uds connection
    Open UDS Connection
    Using pdx

Disconnect
    Log    Close uds connection
    Close UDS Connection

Using pdx
    Load PDX    ${FILE}    ${VARIANT}

*** Test Cases ***
Test user can use Tester Present service on ECU
    Log    Use Tester Present service
    ${response}=    Tester Present

Test user can use ECU Reset service on ECU
    Log    Use ECU Reset service

    Log    Hard reset
    ${response_hr}=    ECU Reset    1

    Log    Key off on reset
    ${response_k}=     ECU Reset    2

    Log    Soft reset
    ${response_sr}=    ECU Reset    3

    Log    Enable rapid power shut down
    ${response_e}=     ECU Reset    4

    Log    disable rapid power shut down
    ${response_d}=     ECU Reset    5

Test user can use Read Data By Name service on ECU
    Log    Use Read Data By Name service

    Log    readCPUClockFrequencies_Read

    ${service_name_list}=    Create List    readCPUClockFrequencies_Read
    Read Data By Name    ${service_name_list}

Test user can use Routine Control By Name service on ECU
    Log    Routine Control By Name service

    Log    PingTest_Start_NoResponse
    Routine Control By Name    PingTest_Start_NoResponse

Test user can use Diagnostic Session Control service on ECU
    Log    Diagnostic Session Control service

    Diagnostic Session Control    1

Test user can use Write Data By Name service on ECU
    Log    Write Data By Name service

    Log    RealTimeClock_Write
    ${PARAM_DICT}=    Create Dictionary    Day=26    Month=September    Year=2024    Hour=10    Second=45    Minute=0
    Write Data By Name    RealTimeClock_Write    ${PARAM_DICT}

    ${DICT}=    Create Dictionary    ipAddress=155
    Write Data By Name    CTS_IPAddress_Write    ${DICT}

    Log    Using service did instead service's name
    Write Data By Identifier    25382    ${PARAM_DICT}
\end{robotcode}



\begin{robotcode}
*** Settings ***
Library    RobotFramework_TestsuitesManagement    WITH NAME    testsuites
Library    RobotFramework_UDS

*** Variables ***
${SUT_IP_ADDRESS_1}=         ecu_ip_address
${SUT_LOGICAL_ADDRESS_1}=    ecu_logical_address
${TB_IP_ADDRESS_1}=          client_ip_address
${TB_LOGICAL_ADDRESS_1}=     client_logical_address
${ACTIVATION_TYPE_1}=        0
${DEVICE_NAME_1}=            UDS Connector 1
${FILE_1}=                   path/CTS_STLA_V1_15_2.pdx
${VARIANT_1}=                CTS_STLA_Brain

${SUT_IP_ADDRESS_2}=         ecu_ip_address
${SUT_LOGICAL_ADDRESS_2}=    ecu_logical_address
${TB_IP_ADDRESS_2}=          client_ip_address
${TB_LOGICAL_ADDRESS_2}=     client_logical_address
${ACTIVATION_TYPE_2}=        0
${DEVICE_NAME_2}=            UDS Connector 2
${FILE_2}=                   path/XTS_S32G_1.0.356.pdx
${VARIANT_2}=                XTS_S32G

${ERROR_STR}=    NegativeResponseException: ReadDataByIdentifier service execution returned a negative response IncorrectMessageLengthOrInvalidFormat (0x13)
*** Test Cases ***
Test user can connect single UDS connection
    Log    Test user can connect single UDS connection
    Log    If no device_name is provided, it will default to 'default'

    Create UDS Connector    ecu_ip_address= ${SUT_IP_ADDRESS_1}
    ...                     ecu_logical_address= ${SUT_LOGICAL_ADDRESS_1}
    ...                     client_ip_address= ${TB_IP_ADDRESS_1}
    ...                     client_logical_address= ${TB_LOGICAL_ADDRESS_1}
    ...                     activation_type= ${ACTIVATION_TYPE_1}

    Connect UDS Connector
    Open UDS Connection
    Load PDX    ${FILE_1}    ${VARIANT_1}
    ${service_name_list}=    Create List    readCPUClockFrequencies_Read
    Read Data By Name    ${service_name_list}
    Close UDS Connection

Test user can connect multiple UDS connection
    Log    Test user can connect multiple UDS connection
    Log    Connect to device 1
    Create UDS Connector    device_name= ${DEVICE_NAME_1}
    ...                     ecu_ip_address= ${SUT_IP_ADDRESS_1}
    ...                     ecu_logical_address= ${SUT_LOGICAL_ADDRESS_1}
    ...                     client_ip_address= ${TB_IP_ADDRESS_1}
    ...                     client_logical_address= ${TB_LOGICAL_ADDRESS_1}
    ...                     activation_type= ${ACTIVATION_TYPE_1}
    Connect UDS Connector   device_name= ${DEVICE_NAME_1}

    Open UDS Connection    device_name= ${DEVICE_NAME_1}
    Load PDX    ${FILE_1}    ${VARIANT_1}    device_name= ${DEVICE_NAME_1}
    ${service_name_list_1}=    Create List    readCPUClockFrequencies_Read
    Read Data By Name    ${service_name_list_1}    device_name= ${DEVICE_NAME_1}

    Log    Connect to device 2
    Create UDS Connector    device_name= ${DEVICE_NAME_2}
    ...                     ecu_ip_address= ${SUT_IP_ADDRESS_2}
    ...                     ecu_logical_address= ${SUT_LOGICAL_ADDRESS_2}
    ...                     client_ip_address= ${TB_IP_ADDRESS_2}
    ...                     client_logical_address= ${TB_LOGICAL_ADDRESS_2}
    ...                     activation_type= ${ACTIVATION_TYPE_2}
    Connect UDS Connector    device_name= ${DEVICE_NAME_2}

    Open UDS Connection    device_name= ${DEVICE_NAME_2}
    Load PDX    ${FILE_2}    ${VARIANT_2}    device_name= ${DEVICE_NAME_2}
    ${service_name_list_2}=    Create List    CPULoad_Read
    Log    Expected device 2 cannot send readCPUClockFrequencies_Read service like device 1
    Run Keyword And Expect Error    ${ERROR_STR}    Read Data By Name    ${service_name_list_1}    device_name= ${DEVICE_NAME_2}

    Read Data By Name    ${service_name_list_2}    device_name= ${DEVICE_NAME_2}

Test user can connect multiple UDS connection but connect to the same CPU
    Log    Test user can connect multiple UDS connection
    Log    Connect to device 1
    Create UDS Connector    device_name= ${DEVICE_NAME_1}
    ...                     ecu_ip_address= ${SUT_IP_ADDRESS_1}
    ...                     ecu_logical_address= ${SUT_LOGICAL_ADDRESS_1}
    ...                     client_ip_address= ${TB_IP_ADDRESS_1}
    ...                     client_logical_address= ${TB_LOGICAL_ADDRESS_1}
    ...                     activation_type= ${ACTIVATION_TYPE_1}
    Connect UDS Connector    device_name= ${DEVICE_NAME_1}
    
    Log    Open uds connection
    Open UDS Connection    device_name= ${DEVICE_NAME_1}
    Load PDX    ${FILE_1}    ${VARIANT_1}    device_name= ${DEVICE_NAME_1}
    ${service_name_list_1}=    Create List    readCPUClockFrequencies_Read
    Read Data By Name    ${service_name_list_1}    device_name= ${DEVICE_NAME_1}

    Log    Connect to device 2 but same IP as device 1
    Log    The expected test case result in an error
    Run Keyword And Expect Error    TimeoutError: ECU failed to respond in time    Create UDS Connector    device_name= ${DEVICE_NAME_2}
    ...                                                                                                    ecu_ip_address= ${SUT_IP_ADDRESS_1}
    ...                                                                                                    ecu_logical_address= ${SUT_LOGICAL_ADDRESS_1}
    ...                                                                                                    client_ip_address= ${TB_IP_ADDRESS_1}
    ...                                                                                                    client_logical_address= ${TB_LOGICAL_ADDRESS_1}
    ...                                                                                                    activation_type= ${ACTIVATION_TYPE_1}

Test users can reconnect to the closed ECU
    Log    Connect to device 2
    Create UDS Connector    device_name= ${DEVICE_NAME_2}
    ...                     ecu_ip_address= ${SUT_IP_ADDRESS_2}
    ...                     ecu_logical_address= ${SUT_LOGICAL_ADDRESS_2}
    ...                     client_ip_address= ${TB_IP_ADDRESS_2}
    ...                     client_logical_address= ${TB_LOGICAL_ADDRESS_2}
    ...                     activation_type= ${ACTIVATION_TYPE_1}
    Connect UDS Connector    device_name= ${DEVICE_NAME_2}

    Open UDS Connection    device_name= ${DEVICE_NAME_2}
    Load PDX    ${FILE_2}    ${VARIANT_2}    device_name= ${DEVICE_NAME_2}
    ${service_name_list_2}=    Create List    CPULoad_Read
    Read Data By Name    ${service_name_list_2}    device_name= ${DEVICE_NAME_2}
    Close UDS Connection    device_name= ${DEVICE_NAME_2}

    Log    Connect to device 1
    Create UDS Connector    device_name= ${DEVICE_NAME_1}
    ...                     ecu_ip_address= ${SUT_IP_ADDRESS_1}
    ...                     ecu_logical_address= ${SUT_LOGICAL_ADDRESS_1}
    ...                     client_ip_address= ${TB_IP_ADDRESS_1}
    ...                     client_logical_address= ${TB_LOGICAL_ADDRESS_1}
    ...                     activation_type= ${ACTIVATION_TYPE_1}
    Connect UDS Connector    device_name= ${DEVICE_NAME_1}

    Open UDS Connection    device_name= ${DEVICE_NAME_1}
    Load PDX    ${FILE_1}    ${VARIANT_1}    device_name= ${DEVICE_NAME_1}
    ${service_name_list_1}=    Create List    readCPUClockFrequencies_Read
    Read Data By Name    ${service_name_list_1}    device_name= ${DEVICE_NAME_1}
    Close UDS Connection    device_name= ${DEVICE_NAME_1}


    Log    Re-opent uds connection device 2
    Open UDS Connection    device_name= ${DEVICE_NAME_2}
    Read Data By Name    ${service_name_list_2}    device_name= ${DEVICE_NAME_2}

    Log    Expected device 2 cannot send readCPUClockFrequencies_Read service like device 1
    Run Keyword And Expect Error    ${ERROR_STR}    Read Data By Name    ${service_name_list_1}    device_name= ${DEVICE_NAME_2}
    Close UDS Connection    device_name= ${DEVICE_NAME_2}
\end{robotcode}