% Copyright 2020-2024 Robert Bosch GmbH

% Licensed under the Apache License, Version 2.0 (the "License");
% you may not use this file except in compliance with the License.
% You may obtain a copy of the License at

% http://www.apache.org/licenses/LICENSE-2.0

% Unless required by applicable law or agreed to in writing, software
% distributed under the License is distributed on an "AS IS" BASIS,
% WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
% See the License for the specific language governing permissions and
% limitations under the License.

% --------------------------------------------------------------------------------------------------------------

\section{Overview}
The \pkg\ is designed to interface with automotive ECUs using the UDS protocol 
over the DoIP (Diagnostic over IP) transport layer. 
This library abstracts the complexities of UDS communication, allowing users to 
focus on writing high-level test cases that validate specific diagnostic 
services and responses.

\section{UDS Connector (DoIP)}
Currently, the library supports the \rcode{DoIP} (Diagnostic over IP) transport 
layer, which is commonly used in modern vehicles for diagnostic communication. 
DoIP allows for faster data transfer rates and easier integration with 
network-based systems compared to traditional CAN-based diagnostics.

\section{Configuration}
In order to connect and send/receive message properly using the \pkg\, 
certain configurations must be set up:

\begin{itemize}
   \item DoIP Configuration:
         The library requires the IP address and port of the ECU or the gateway 
         through which the ECU is accessed.
   \item Data Identifiers and Codec:
         Define the Data Identifiers (DIDs) and corresponding codecs in the 
         libraryâ€™s configuration. 
         This enables correct encoding and decoding of data between the test 
         cases and the ECU.
   \item Session Management: 
         Some UDS services may require the ECU to be in a specific diagnostic 
         session (e.g., extended diagnostics). 
         The library should be configured to manage these session transitions 
         seamlessly.
\end{itemize}

\section{Supported UDS Services}
The \pkg\ library supports almost UDS service as defined in 
\href{https://automotive.wiki/index.php/ISO_14229}{ISO 14229}, 
providing comprehensive coverage for ECU diagnostics.

For detailed information on specific services and how to use them, please refer 
to the next section.

\section{Enhancements Usability with ODXTools Integration}
The \pkg\ library comes with 
\href{https://github.com/mercedes-benz/odxtools}{odxtools} fully integrated, 
allowing you to use readable service names instead of dealing with hex IDs.

You can now specify service names directly in your test cases, 
making them more readable and user-friendly.

The \pkg\ also supports automatic data type conversion for request parameters, 
enabling users to provide input values that are seamlessly converted into the 
correct data types required for UDS requests.

The \pkg\ library supports features such as Routine Control By Name, 
Read Data By Name, and Write Data By Name, enabling users to send requests to 
UDS using the service's name instead of its identifier.

{\large\textbf{Examples}}

\textbf{Routine Control By Name}
\begin{robotcode}
Test user can use Routine Control By Name service on ECU
    Log    Routine Control By Name service: StartIperfServer_Start

    ${param_dict}=    Create Dictionary    port=5101    argument=-i 0.5 -B 192.168.1.
    ${response}=    Routine Control By Name    StartIperfServer_Start    ${param_dict}

    Log    ${response}    console=True
    FOR    ${item}    IN    @{response.keys()}
        Log    ${item} : ${response["${item}"]}    console=True
    END
\end{robotcode}

\textbf{Read Data By Name}
\begin{robotcode}
Test user can use Read Data By Name service on ECU
    Log    Use Read Data By Name service

    Log    Use Read Data By Identifier - 25392

    ${list_identifers}=    Create List    0x6330
    ${res}=    Read Data By Identifier    ${list_identifers}
    Log    ${res}    console=True
    Log    ${res[0x6330]}    console=True

    Log    readCPUClockFrequencies_Read

    ${service_name_list}=    Create List    readCPUClockFrequencies_Read
    ${responses}=    Read Data By Name    ${service_name_list}
    Log    ${responses}    console=True

    FOR    ${request_did}    IN    @{responses.keys()}
        Log    Key: ${request_did}, Value: ${responses["${request_did}"]}    console=True
        ${response}=    Set Variable    ${responses["${request_did}"]}
        FOR    ${item}    IN    @{response.keys()}
            Log    ${item} : ${response["${item}"]}    console=True
        END
    END

Test Read List Services
    ${list_read_services}=    Create List   TestManager_SWVersion_Read
    ...                                     CTSSWVersion_Read
    ...                                     CPULoad_Read

    FOR    ${service_name}    IN    @{list_read_services}
        Log      Read Data of ${service_name}     console=True
        ${list_service}=    Create List    ${service_name}
        ${res}=    Read Data By Name   ${list_service}
        Log    ${res}    console=True
    END
\end{robotcode}

\textbf{Write Data By Name}
\begin{robotcode}
Test user can use Write Data By Name service on ECU
    Log    Write Data By Name service

    Log    RealTimeClock_Write
    ${PARAM_DICT}=    Create Dictionary    Day=26    Month=September    Year=2024    Hour=10    Second=45    Minute=0
    ${res}=     Write Data By Name    RealTimeClock_Write    ${PARAM_DICT}
    Log    ${res}    console=True

    ${DICT}=    Create Dictionary    ipAddress=155
    ${res}=    Write Data By Name    CTS_IPAddress_Write    ${DICT}
    Log    ${res}    console=True

    Log    Using service did instead service's name
    ${res}=    Write Data By Identifier    25382    ${PARAM_DICT}
    Log    ${res}    console=True
\end{robotcode}

\section{Multiple Connections}
The \pkg\ can be extended to manage multiple connections simultaneously.
This is beneficial when working with complex vehicle systems or simultaneously
testing multiple ECUs.

\section{Examples}
\textbf{Single Connection}
\begin{robotcode}
*** Settings ***
Library    RobotFramework_TestsuitesManagement    WITH NAME    testsuites
Library    RobotFramework_UDS

*** Variables ***
${SUT_IP_ADDRESS_1}=    192.168.0.7
${SUT_LOGICAL_ADDRESS_1}=    1863
${TB_IP_ADDRESS_1}=    192.168.0.240
${TB_LOGICAL_ADDRESS_1}=    1895
${ACTIVATION_TYPE_1}=    0
${DEVICE_NAME_1}=    UDS Connector 1
${FILE_1}=    C:/Users/MAR3HC/Desktop/UDS/robotframework-uds/test/pdx/CTS_STLA_V1_15_2.pdx
${VARIANT_1}=    CTS_STLA_Brain

*** Test Cases ***
Test user can connect single UDS connection
    Log    Test user can connect single UDS connection
    Log    If no device_name is provided, it will default to 'default'

    Create UDS Connector    ecu_ip_address= ${SUT_IP_ADDRESS_1}
    ...                     ecu_logical_address= ${SUT_LOGICAL_ADDRESS_1}
    ...                     client_ip_address= ${TB_IP_ADDRESS_1}
    ...                     client_logical_address= ${TB_LOGICAL_ADDRESS_1}
    ...                     activation_type= ${ACTIVATION_TYPE_1}

    Connect UDS Connector
    Open UDS Connection
    Load PDX    ${FILE_1}    ${VARIANT_1}
    ${service_name_list}=    Create List    readCPUClockFrequencies_Read
    Read Data By Name    ${service_name_list}
    Close UDS Connection
\end{robotcode}

\textbf{Multiple Connections}
\begin{robotcode}
*** Variables ***
${SUT_IP_ADDRESS_1}=    192.168.0.7
${SUT_LOGICAL_ADDRESS_1}=    1863
${TB_IP_ADDRESS_1}=    192.168.0.240
${TB_LOGICAL_ADDRESS_1}=    1895
${ACTIVATION_TYPE_1}=    0
${DEVICE_NAME_1}=    UDS Connector 1
${FILE_1}=    C:/Users/MAR3HC/Desktop/UDS/robotframework-uds/test/pdx/CTS_STLA_V1_15_2.pdx
${VARIANT_1}=    CTS_STLA_Brain

${SUT_IP_ADDRESS_2}=    192.168.0.4
${SUT_LOGICAL_ADDRESS_2}=    1863
${TB_IP_ADDRESS_2}=    192.168.0.240
${TB_LOGICAL_ADDRESS_2}=    1895
${ACTIVATION_TYPE_2}=    0
${DEVICE_NAME_2}=    UDS Connector 2
${FILE_2}=    C:/Data/Git/mpci_rack_nau2kor/project/testbench/hi1/uds/XTS_S32G_1.0.356.pdx
${VARIANT_2}=    XTS_S32G

${ERROR_STR}=    NegativeResponseException: ReadDataByIdentifier service execution returned a negative response IncorrectMessageLengthOrInvalidFormat (0x13)

*** Test Cases ***
Test user can connect multiple UDS connection
    Log    Test user can connect multiple UDS connection
    Log    Connect to device 1
    Create UDS Connector    device_name= ${DEVICE_NAME_1}
    ...                     ecu_ip_address= ${SUT_IP_ADDRESS_1}
    ...                     ecu_logical_address= ${SUT_LOGICAL_ADDRESS_1}
    ...                     client_ip_address= ${TB_IP_ADDRESS_1}
    ...                     client_logical_address= ${TB_LOGICAL_ADDRESS_1}
    ...                     activation_type= ${ACTIVATION_TYPE_1}
    Connect UDS Connector   device_name= ${DEVICE_NAME_1}

    Open UDS Connection    device_name= ${DEVICE_NAME_1}
    Load PDX    ${FILE_1}    ${VARIANT_1}    device_name= ${DEVICE_NAME_1}
    ${service_name_list_1}=    Create List    readCPUClockFrequencies_Read
    Read Data By Name    ${service_name_list_1}    device_name= ${DEVICE_NAME_1}

    Log    Connect to device 2
    Create UDS Connector    device_name= ${DEVICE_NAME_2}
    ...                     ecu_ip_address= ${SUT_IP_ADDRESS_2}
    ...                     ecu_logical_address= ${SUT_LOGICAL_ADDRESS_2}
    ...                     client_ip_address= ${TB_IP_ADDRESS_2}
    ...                     client_logical_address= ${TB_LOGICAL_ADDRESS_2}
    ...                     activation_type= ${ACTIVATION_TYPE_2}
    Connect UDS Connector    device_name= ${DEVICE_NAME_2}

    Open UDS Connection    device_name= ${DEVICE_NAME_2}
    Load PDX    ${FILE_2}    ${VARIANT_2}    device_name= ${DEVICE_NAME_2}
    ${service_name_list_2}=    Create List    CPULoad_Read
    Log    Expected device 2 cannot send readCPUClockFrequencies_Read service like device 1
    Run Keyword And Expect Error    ${ERROR_STR}    Read Data By Name    ${service_name_list_1}    device_name= ${DEVICE_NAME_2}

    Read Data By Name    ${service_name_list_2}    device_name= ${DEVICE_NAME_2}

Test user can connect multiple UDS connection but connect to the same ECU
    Log    Test user can connect multiple UDS connection
    Log    Connect to device 1
    Create UDS Connector    device_name= ${DEVICE_NAME_1}
    ...                     ecu_ip_address= ${SUT_IP_ADDRESS_1}
    ...                     ecu_logical_address= ${SUT_LOGICAL_ADDRESS_1}
    ...                     client_ip_address= ${TB_IP_ADDRESS_1}
    ...                     client_logical_address= ${TB_LOGICAL_ADDRESS_1}
    ...                     activation_type= ${ACTIVATION_TYPE_1}
    Connect UDS Connector    device_name= ${DEVICE_NAME_1}

    Log    Open uds connection
    Open UDS Connection    device_name= ${DEVICE_NAME_1}
    Load PDX    ${FILE_1}    ${VARIANT_1}    device_name= ${DEVICE_NAME_1}
    ${service_name_list_1}=    Create List    readCPUClockFrequencies_Read
    Read Data By Name    ${service_name_list_1}    device_name= ${DEVICE_NAME_1}

    Log    Connect to device 2 but same IP as device 1
    Log    The expected test case result in an error
    Run Keyword And Expect Error    TimeoutError: ECU failed to respond in time    Create UDS Connector    device_name= ${DEVICE_NAME_2}
    ...                                                                                                    ecu_ip_address= ${SUT_IP_ADDRESS_1}
    ...                                                                                                    ecu_logical_address= ${SUT_LOGICAL_ADDRESS_1}
    ...                                                                                                    client_ip_address= ${TB_IP_ADDRESS_1}
    ...                                                                                                    client_logical_address= ${TB_LOGICAL_ADDRESS_1}
    ...                                                                                                    activation_type= ${ACTIVATION_TYPE_1}

Test users can reconnect to the closed ECU
    Log    Connect to device 2
    Create UDS Connector    device_name= ${DEVICE_NAME_2}
    ...                     ecu_ip_address= ${SUT_IP_ADDRESS_2}
    ...                     ecu_logical_address= ${SUT_LOGICAL_ADDRESS_2}
    ...                     client_ip_address= ${TB_IP_ADDRESS_2}
    ...                     client_logical_address= ${TB_LOGICAL_ADDRESS_2}
    ...                     activation_type= ${ACTIVATION_TYPE_1}
    Connect UDS Connector    device_name= ${DEVICE_NAME_2}

    Open UDS Connection    device_name= ${DEVICE_NAME_2}
    Load PDX    ${FILE_2}    ${VARIANT_2}    device_name= ${DEVICE_NAME_2}
    ${service_name_list_2}=    Create List    CPULoad_Read
    Read Data By Name    ${service_name_list_2}    device_name= ${DEVICE_NAME_2}
    Close UDS Connection    device_name= ${DEVICE_NAME_2}

    Log    Connect to device 1
    Create UDS Connector    device_name= ${DEVICE_NAME_1}
    ...                     ecu_ip_address= ${SUT_IP_ADDRESS_1}
    ...                     ecu_logical_address= ${SUT_LOGICAL_ADDRESS_1}
    ...                     client_ip_address= ${TB_IP_ADDRESS_1}
    ...                     client_logical_address= ${TB_LOGICAL_ADDRESS_1}
    ...                     activation_type= ${ACTIVATION_TYPE_1}
    Connect UDS Connector    device_name= ${DEVICE_NAME_1}

    Open UDS Connection    device_name= ${DEVICE_NAME_1}
    Load PDX    ${FILE_1}    ${VARIANT_1}    device_name= ${DEVICE_NAME_1}
    ${service_name_list_1}=    Create List    readCPUClockFrequencies_Read
    Read Data By Name    ${service_name_list_1}    device_name= ${DEVICE_NAME_1}
    Close UDS Connection    device_name= ${DEVICE_NAME_1}


    Log    Re-opent uds connection device 2
    Open UDS Connection    device_name= ${DEVICE_NAME_2}
    Read Data By Name    ${service_name_list_2}    device_name= ${DEVICE_NAME_2}

    Log    Expected device 2 cannot send readCPUClockFrequencies_Read service like device 1
    Run Keyword And Expect Error    ${ERROR_STR}    Read Data By Name    ${service_name_list_1}    device_name= ${DEVICE_NAME_2}
    Close UDS Connection    device_name= ${DEVICE_NAME_2}
\end{robotcode}

\textbf{Routine Control By Name}
\begin{robotcode}
Test user can use Routine Control By Name service on ECU
    Log    Routine Control By Name service: StartIperfServer_Start

    ${param_dict}=    Create Dictionary    port=5101    argument=-i 0.5 -B 192.168.1.
    ${response}=    Routine Control By Name    StartIperfServer_Start    ${param_dict}

    Log    ${response}    console=True
    FOR    ${item}    IN    @{response.keys()}
        Log    ${item} : ${response["${item}"]}    console=True
    END
\end{robotcode}

\textbf{Read Data By Name}
\begin{robotcode}
Test user can use Read Data By Name service on ECU
    Log    Use Read Data By Name service

    Log    Use Read Data By Identifier - 25392

    ${list_identifers}=    Create List    0x6330
    ${res}=    Read Data By Identifier    ${list_identifers}
    Log    ${res}    console=True
    Log    ${res[0x6330]}    console=True

    Log    readCPUClockFrequencies_Read

    ${service_name_list}=    Create List    readCPUClockFrequencies_Read
    ${responses}=    Read Data By Name    ${service_name_list}
    Log    ${responses}    console=True

    FOR    ${request_did}    IN    @{responses.keys()}
        Log    Key: ${request_did}, Value: ${responses["${request_did}"]}    console=True
        ${response}=    Set Variable    ${responses["${request_did}"]}
        FOR    ${item}    IN    @{response.keys()}
            Log    ${item} : ${response["${item}"]}    console=True
        END
    END

Test Read List Services
    ${list_read_services}=    Create List   TestManager_SWVersion_Read
    ...                                     CTSSWVersion_Read
    ...                                     CPULoad_Read

    FOR    ${service_name}    IN    @{list_read_services}
        Log      Read Data of ${service_name}     console=True
        ${list_service}=    Create List    ${service_name}
        ${res}=    Read Data By Name   ${list_service}
        Log    ${res}    console=True
    END
\end{robotcode}

\textbf{Write Data By Name}
\begin{robotcode}
Test user can use Write Data By Name service on ECU
    Log    Write Data By Name service

    Log    RealTimeClock_Write
    ${PARAM_DICT}=    Create Dictionary    Day=26    Month=September    Year=2024    Hour=10    Second=45    Minute=0
    ${res}=     Write Data By Name    RealTimeClock_Write    ${PARAM_DICT}
    Log    ${res}    console=True

    ${DICT}=    Create Dictionary    ipAddress=155
    ${res}=    Write Data By Name    CTS_IPAddress_Write    ${DICT}
    Log    ${res}    console=True

    Log    Using service did instead service's name
    ${res}=    Write Data By Identifier    25382    ${PARAM_DICT}
    Log    ${res}    console=True
\end{robotcode}

\textbf{Get Encoded Request Message}
\begin{robotcode}
Test Get Encoded Request Message - Simple request parameters
    Log     Test Get Encoded Request Message - mainCPUStressTest_Start
    ${service_name}=    Set Variable    mainCPUStressTest_Start
    ${param_dict}=    Create Dictionary    cores=5    load=50
    ${res}=    Get Encoded Request Message    ${service_name}    ${param_dict}
    Log    ${res}    console=True
    Log    ${res.hex()}    console=True

Test Get Encoded Request Message - Complex request parameters
    Load PDX    C:/Users/MAR3HC/Desktop/UDS/robotframework-uds/test/pdx/XTS_MPCI_Maas_1.23.45.pdx    XTS_MPCI_MaaS

    Log     Test Get Encoded Request Message - CAN_MasterSlaveEnduranceRun_Start
    ${service_name}=    Set Variable    CAN_MasterSlaveEnduranceRun_Start
    ${res}=    Get Encoded Request Message    ${service_name}    ${canConfig}
    Log    ${res}    console=True
    Log    ${res.hex()}    console=True
\end{robotcode}

\textbf{Get Decoded Request Message}
\begin{robotcode}
Test Get Decoded Response Message
    Log     Test Get Decoded Response Message
    ${service_name}=    Set Variable    StartIperfServer_Start
    ${response_str}=    Set Variable    \x012#\x00\x00\x18\x05H
    ${response_byte}=    Convert To Bytes    ${response_str}
    ${res}=    Get Decoded Response Message    ${service_name}    ${response_byte}
    Log    ${res}    console=True
\end{robotcode}
